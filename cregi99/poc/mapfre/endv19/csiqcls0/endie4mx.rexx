/* REXX - EDIT MACRO ENDIE4MX (ALIAS COMPELM) TO COMPARE THE
          CURRENT ELEMENT ANOTHER, OR EVEN THE SAME ELEMENT
          LOCATED UP THE MAP.  UNDER THE COVERS COMPELM
          INVOKES COPYELM TO INSERT THE TARGET ELEMENT INTO
          A TEMPORARY DATASET AND THEN INVOKES THE BUILTIN
          COMPARE ROUTINE.

   PARAMETERS:
          COMPELM CAN BE INVOKED PASSING THE ELEMENT NAME AND TYPE
          OR USING '=' FOR EITHER/BOTH PARMS TO MEAN THE
          CURRENT ELEMENT NAME AND/OR TYPE.
          COMPELM ALSO SUPPORTS "NEXT" WHICH WILL DRIVE A SELECTION
          LIST FOR THE CURRENT ELEMENT/TYPE LOOKING UP THE MAP, OR...
          ALL TO LOOK FOR OTHER VERSIONS OF THIS ELEMENT IN ALL SUBSYSTEMS
          OTHER PARMS (LIKE X, SYSIN, ETC.) ARE PASSED THROUGH TO THE
          BUILTIN COMPARE COMMAND.
*/
TRACE O
ADDRESS ISREDIT
"MACRO (PARMS) PROCESS"
IF RC > 0 THEN EXIT RC
"(DSN) = DATASET"             /* GET DATASET NAME */
"(MEM) = MEMBER"              /* EXPECT TO BE BLANK */
"(LRC) = LRECL"               /* GET RECORD LENGTH  */
"(BLK) = BLKSIZE"             /* GET BLOCKSIZE      */
"(RFM,RFB) = RECFM"           /* GET RECORD FORMAT  */
"(VOL) = VOLUME"              /* GET VOLUME         */
"(DTD) = DATAID"              /* GET DATAAID        */
IF RFM = "V" THEN LRC = LRC + 4 /* ALLOW FOR RDW */
SA= "DS:" DSN "MEM:" MEM "PARMS:" PARMS
SA= "LR:" LRC "BLK:" BLK "RECFM:" RFM "VOL:" VOL "DTD:" DTD
TEMPDSN = DSN||".COMP"
IF SYSDSN("'"TEMPDSN"'") = "OK" THEN
   DO
      SA= "REUSING DATASET"
   END
ELSE
   DO
      TEMPFIL = COMP||RANDOM(9999)
      ADDRESS TSO "ALLOC FILE("TEMPFIL") DS('"TEMPDSN"') RECFM("RFM","RFB")",
          "LRECL("LRC") BLKSIZE("BLK") LIKE('"DSN"') NEW REU CATALOG"
      IF RC > 0 THEN DO            /* CAN'TDO ANYTHING IF CAN'T ALLOC FILE */
         ZEDSMSG = "ERROR ALLOCATING TEMPDSN"
         ZEDLMSG = "UNABLE TO ALLOC FILE("TEMPFIL") TO DATASET('"TEMPDSN"')",
                   "RC:" RC
         ADDRESS ISPEXEC "SETMSG MSG(ISRZ000)"
         EXIT 12
      END
   END
SA= "PARSING PARMS"
ADDRESS ISPEXEC "VGET (ENVELM ENVBTYP)"         /* GET ELEMENT & TYPE */
VALIDPARMS = "X EXCLUDE SYSIN SYSIN SAVE"
IGNORPARMS = "VOLSER SESSION *"
COMPPARM   = ""   /* START WITH NOTHING */
NAMEPARM   = ""
TYPEPARM   = ""
OTHRPARM   = ""

DO I = 1 TO WORDS(PARMS)
   THISWORD = TRANSLATE(WORD(PARMS,I))
   BRKTPOS = POS('(',THISWORD)
   IF BRKTPOS > 1 THEN DO
      THISWORD = LEFT(THISWORD,BRKTPOS-1)
   END
   SELECT
     WHEN WORDPOS(THISWORD,VALIDPARMS) > 0 THEN
       COMPPARM = COMPPARM WORD(PARMS,I)
     WHEN WORDPOS(THISWORD,IGNORPARMS) > 0 THEN
       SA= "PARM:" WORD(PARMS,I) "IS NOT VALID FOR COMPELM, IGNORED"
     WHEN THISWORD ==  'NEXT' THEN DO
       SA= "USER WANTS NEXT UP THE MAP"
       IF WORDS(OTHRPARM) = 0 THEN DO
          NAMEPARM = ENVELM          /* SET NAME FOR COPYELM */
          TYPEPARM = ENVBTYP         /* SET TYPE FOR COPYELM */
          END
       END
     WHEN THISWORD ==  'ALL' THEN DO
       SA= "USER WANTS ALL SANDBOXES "
       IF WORDS(OTHRPARM) = 0 THEN DO
          NAMEPARM = ENVELM          /* SET NAME FOR COPYELM */
          TYPEPARM = ENVBTYP         /* SET TYPE FOR COPYELM */
          COMPALLB = 'Y'
          ADDRESS ISPEXEC "VPUT (COMPALLB) SHARED"
          END
       END
     WHEN BRKTPOS = 1 THEN DO
       SA= "USER PASSING A NAME IN BRACKETS"
       NAMEPARM = STRIP(SUBSTR(WORD(PARMS,I),2),'T',')')
       END
   OTHERWISE /* USER IS PASSING A NAME/TYPE/... */
     OTHRPARM = OTHRPARM WORD(PARMS,I)
   END
END
COPYPARM = STRIP(NAMEPARM TYPEPARM OTHRPARM)

/* DEBUG PARSE RESULTS
   SAY "COMP PARM:" COMPPARM
   SAY "COPY PARM:" COPYPARM
   SAY "NAME PARM:" NAMEPARM
   SAY "TYPE PARM:" TYPEPARM
   SAY "OTHR PARM:" OTHRPARM
*/

SA= "CALLING EDIT"

ADDRESS ISPEXEC "EDIT DATASET('"TEMPDSN"') MACRO(ENDIE4M2) PARM(COPYPARM)"
SA= "RC FROM EDIT:" RC
IF COMPALLB = 'Y' THEN DO                /* DID USER REQUEST ALL? */
   ADDRESS ISPEXEC "VERASE (COMPALLB) SHARED" /* YES - THEN CLEANUP */
END
ADDRESS ISPEXEC "VGET (COMPELM#) SHARED" /* GET NO.LINES FROM COPYELM */
IF COMPELM# > 0 THEN DO                  /* HAVE WE GOT LINES TO COMPARE? */
SA= "CALLING COMP"
/* CALL THE BUILTIN COMPARE COMMAND - PASSING ANY VALID PARMS */
"BUILTIN COMPARE '"TEMPDSN"'" COMPPARM
END

/* EXIT AND CLEAN UP */
X = OUTTRAP('NFMSG')
ADDRESS TSO "FREE FILE("TEMPFIL")"
ADDRESS TSO "DELETE '"TEMPDSN"'"
Y = OUTTRAP('OFF')
