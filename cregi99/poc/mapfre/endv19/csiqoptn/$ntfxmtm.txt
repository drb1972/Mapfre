         MACRO
         $NTFXMTM &TYPE=W,&PRIM=10,&SEC=5,&SIZ=84
         LCLC &N
&N       SETC '&SYSNDX'
.*
.*   MACRO $NTFXMTM COMES IN ADDITION TO THE $NOTIFY MACRO
.*            IT IS INTENDED TO BE USED WITH THE NEW XMITM PROTOCOL
.*            AND BC1PNTFY MODULE IN ORDER TO TRANSMIT SEVERAL LINES
.*            OF MESSAGES.
.*
.*   TYPE=I : ALLOCATES CPOOL TO CREATE ROOM  FOR MESSAGE LINES
.*            PRIM : NUMBER OF CELLS (ONE CELL EACH LINE)
.*            SEC  : WHEN THE NUMBER OF LINES GETS GREATER THAN PRIM,
.*                   ADDS MORE CELLS .
.*
.*   TYPE=W : ACQUIRES A CELL
.*            MOVES NOTMSGXT IN THIS CELL (MESSAGE FOR XMIT)
.*
.*   TYPE=C : FREES THE CPOOL
.*
.*
         AIF  ('&TYPE' EQ 'W').WRITE
         AIF  ('&TYPE' EQ 'I').INIT
         AIF  ('&TYPE' EQ 'C').CLOSE
         MNOTE 8,'*** ERROR PARAM TYPE INVALID'
         MEXIT
.INIT    ANOP
*
         XC    NOTMSGLP,NOTMSGLP       INIT MSG TEXT CHAIN POINTER
         XC    NOTMSGCP,NOTMSGCP       INIT MSG TEXT CHAIN POINTER
         MVC   CPOOLR(LGTCPOOL),CP&SYSNDX
       CPOOL BUILD,PCELLCT=&PRIM,SCELLCT=&SEC,CSIZE=&SIZ,MF=(E,CPOOLR)
         ST    R0,NOTCPID
         B     $I&SYSNDX
CP&N   CPOOL BUILD,PCELLCT=2500,SCELLCT=500,CSIZE=&SIZ,LOC=BELOW,MF=L, X
               HDR='CPOOL BC1PNTFY'
LGTCPOOL EQU   *-CP&SYSNDX
$I&SYSNDX DS   0H
         MEXIT
.CLOSE   ANOP
         OC    NOTCPID,NOTCPID
         BZ    $R&SYSNDX
         CPOOL DELETE,CPID=NOTCPID
         XC    NOTCPID,NOTCPID
         XC    NOTMSGTX,NOTMSGTX
$R&SYSNDX  DS  0H
         MEXIT
.WRITE   ANOP
         CPOOL GET,CPID=NOTCPID        ACQUIRE STORAGE FOR TABLE
         XC    0(4,R1),0(R1)           INITIALIZE NEXT-IN-LIST PTR
         OC    NOTMSGLP,NOTMSGLP       IS FIRST MESSAGE IN THE LIST?
         BNZ   $S&SYSNDX               NO...
         ST    R1,NOTMSGCP             SAVE CURRENT MESSAGE ADDR
         ST    R1,NOTMSGLP             SET FIRST MSG IN LIST ADDR
         B     $R&SYSNDX               GO COPY MESSAGE TEXT
$S&SYSNDX  DS  0H
         L     R15,NOTMSGCP            HOLD "OLD" CURRENT MSG PTR
         ST    R1,NOTMSGCP             UPDATE FIELD WITH NEW MSG PTR
         ST    R1,0(R15)               UPDATE LAST MSG WITH PTR TO NEW
         SPACE ,
$R&SYSNDX  DS  0H
         MVC   4(80,R1),NOTMSGTX                                        00280001
         MEND
