.*
.* DEFEDIT IS USED SET THE LENGTH OF THE DATA VALUE SPECIFED IN THE
.*         GLOBAL VARIABLE &EDITIN.  THE LENGTH IS SET IN THE GLOBAL
.*         VARIABLE &EDITLNG AND THE DATA IS COPIED TO THE GLOBAL
.*         VARIABLE &EDITOUT.
.*
.*         IT CALCULATES THE LENGTH OF FIELD BY COUNTING DOUBLE
.*         AMPERSANDS OR APOSTROPHES AS SINGLE.
.*
         MACRO
&LABEL   DEFEDIT &REMOVEBLANKS=Y
         LCLA  &CHKOFFSET
         GBLC  &EDITIN,&EDITOUT
         GBLA  &EDITLNG,&REALLNG
&EDITOUT SETC  '&EDITIN'
&EDITLNG SETA  K'&EDITIN
         AIF   ('&EDITIN' NE '').QTECHK
.NULL    ANOP
&EDITOUT SETC  ' '
&EDITLNG SETA  0
         AGO   .EDITEND
.QTECHK  ANOP
         AIF   ('&EDITIN'(1,1) NE '''').AMPERS
.*
&EDITLNG SETA  &EDITLNG-2
.*
.* THIS CODE REMOVES TRAILING BLANKS. SUCH CAN ONLY APPEAR IN THE
.* SYMDATA ENCLOSED IN QUOTES.
.*
         AIF   ('&REMOVEBLANKS' EQ 'N' ).ENDBLNK
.BLANKS  ANOP
         AIF   ('&EDITIN'(&EDITLNG+1,1) NE ' ').ENDBLNK
         AIF   (&EDITLNG EQ 1).NULL
&EDITLNG SETA  &EDITLNG-1
         AGO   .BLANKS
.*
.ENDBLNK ANOP
&EDITOUT SETC  '&EDITIN'(2,&EDITLNG)
.*
.AMPERS  ANOP
.* THIS CODE CALCULATES THE LENGTH OF FIELD BY COUNTING DOUBLE
.* AMPERSANDS OR APOSTROPHES AS SINGLE.
&EDITLNG SETA  DCLEN('&EDITOUT')         ADJUST FOR && OR ''
.*
.EDITEND ANOP
         MEND
