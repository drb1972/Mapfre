         MACRO
&LABEL   C1PROPGM &STEPNAM=,&PGM=,&MAXRC=255,&PARM=,                   X
               &DSECT=NO
         GBLA  &RLSTEP#
         GBLC  &GPGM,&RLSTP#L
         LCLA  &PARMLNG
         LCLC  &WSTEP,&WPGM,&WPARM
         AIF   ('&DSECT' EQ 'YES').DODSECT
.*
.*  INCREMENT STEP COUNT AND ORG VALUE OVER C1PROHDR COUNT FIELD
.*
&RLSTEP# SETA &RLSTEP#+1
         ORG   &RLSTP#L
         DC    AL1(&RLSTEP#)
         ORG
&WSTEP   SETC  '&STEPNAM'
&WPGM    SETC  '&PGM'
&GPGM    SETC  '&PGM'
         AIF   ('&STEPNAM' NE '').PGMCHK
&WSTEP   SETC  ' '
.PGMCHK  ANOP
         AIF   ('&PGM' NE '').PRMCHK
&WPGM    SETC  ' '
.PRMCHK  ANOP
&PARMLNG SETA  K'&PARM
         AIF   (&PARMLNG EQ 0).ENDCHK
&WPARM   SETC  '&PARM'
.*
         AIF   ('&WPARM'(1,1) NE '''').PRNCHK
&PARMLNG SETA  K'&WPARM-2
&WPARM   SETC  '&WPARM'(2,&PARMLNG)
.*
.PRNCHK  ANOP
         AIF   ('&WPARM'(1,1) NE '(').ENDCHK
&PARMLNG SETA  K'&WPARM-2
&WPARM   SETC  '&WPARM'(2,&PARMLNG)
.ENDCHK  ANOP
.*
.*
PGM&SYSNDX DC CL200' '
         ORG   PGM&SYSNDX
         DC    XL1'00'                 EXEC STATEMENT IDENTIFIER
         DC    AL1(&RLSTEP#)           RELATIVE STEP NUMBER
         DC    CL8'&WSTEP'             STEP NAME
         DC    CL8'&WPGM'              PROGRAM NAME
         DC    AL1(&MAXRC)             IF RC > &MAXRC, PROCESS FAILS
         DC    AL2(&PARMLNG)           EXEC PARM LENGTH
         AIF   (&PARMLNG EQ 0).NOPARM
         DC    CL100'&WPARM'           EXECUTION PARM
.NOPARM  ANOP
         ORG
         MEXIT
.*
.DODSECT ANOP
C1PROPGM DSECT                         EXEC STATEMENT INFORMATION
C1PROPGM#RECORD#ID DS   X              STATEMENT IDENTIFIER
C1PROPGM#EXEC#ID  EQU   0              EXEC STATEMENT IDENTIFIER
C1PROPGM#STEP#    DS    X              RELATIVE STEP NUMBER
C1PROPGM#STEPNAME DS    CL8            STEP NAME
C1PROPGM#PROGRAM  DS    CL8            PROGRAM NAME
C1PROPGM#FAIL     DS    X              PROC FAILURE COND CODE VALUE
C1PROPGM#PARMLNG  DS    AL2            EXEC PARAMETER LENGTH
C1PROPGM#PARM     DS    CL100          EXEC PARAMETER
                  ORG   C1PROPGM+200
C1PROPGM#DSLN     EQU   *-C1PROPGM
         MEND
