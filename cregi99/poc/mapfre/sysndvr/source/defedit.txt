.*
.* DEFEDIT IS USED SET THE LENGTH OF THE DATA VALUE SPECIFED IN THE
.*         GLOBAL VARIABLE &EDITIN.  THE LENGTH IS SET IN THE GLOBAL
.*         VARIABLE &EDITLNG AND THE DATA IS COPIED TO THE GLOBAL
.*         VARIABLE &EDITOUT.
.*
.*         THE PARAMETER AMPERADJUSTCOUNT=Y/N WAS ADDED TO ENABLE THE
.*         INVOKER OF DEFEDIT TO INDICATE THAT DOUBLE AMPERSANDS FOUND
.*         IN IN THE INPUT DATA FIELD SHOULD ONLY BE COUNTED AS 1.
.*         &&C1SYSTEM = LNG(9) NOT LNG(10)
.*
.*
         MACRO
&LABEL   DEFEDIT &AMPERADJUSTCOUNT=N,&REMOVEBLANKS=Y
         LCLA  &CHKOFFSET
         GBLC  &EDITIN,&EDITOUT
         GBLA  &EDITLNG,&REALLNG
&EDITOUT SETC  '&EDITIN'
&EDITLNG SETA  K'&EDITIN
         AIF   ('&EDITIN' NE '').QTECHK
.NULL    ANOP
&EDITOUT SETC  ' '
&EDITLNG SETA  0
         AGO   .EDITEND
.QTECHK  ANOP
         AIF   ('&EDITIN'(1,1) NE '''').AMPERS
.*
&EDITLNG SETA  &EDITLNG-2
.*
.* THIS CODE REMOVES TRAILING BLANKS. SUCH CAN ONLY APPEAR IN THE
.* SYMDATA ENCLOSED IN QUOTES.
.*
         AIF   ('&REMOVEBLANKS' EQ 'N' ).ENDBLNK
.BLANKS  ANOP
         AIF   ('&EDITIN'(&EDITLNG+1,1) NE ' ').ENDBLNK
         AIF   (&EDITLNG EQ 1).NULL
&EDITLNG SETA  &EDITLNG-1
         AGO   .BLANKS
.*
.ENDBLNK ANOP
&EDITOUT SETC  '&EDITIN'(2,&EDITLNG)
.*
.AMPERS  ANOP
         AIF   ('&AMPERADJUSTCOUNT' EQ 'N').EDITEND
.*
.*
.* GO THROUGH THE TEXT STRING, LOOKING FOR DOUBLE AMPERSANDS
.*   SUBTRACT 1 FROM THE STRING LENGTH (EDITLNG) FOR EACH PAIR FOUND
.*
&CHKOFFSET SETA 1
.*
&REALLNG SETA  &EDITLNG
.AMPER10 ANOP
         AIF   (&CHKOFFSET GE &REALLNG).AMPEREND
         AIF   ('&EDITOUT'(&CHKOFFSET,2) EQ '&&').AMPER20
&CHKOFFSET SETA &CHKOFFSET+1
         AGO   .AMPER10
.*
.AMPER20 ANOP
&EDITLNG SETA  &EDITLNG-1                ADJUST FOR DOUBLE-AMPERSAND
&CHKOFFSET SETA &CHKOFFSET+2             POINT PAST DOUBLE-AMPERSAND
         AGO   .AMPER10
.AMPEREND ANOP
.*
.EDITEND ANOP
         MEND
