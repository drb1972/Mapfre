ENHAAPGM TITLE 'ENDEVOR - API SAMPLE ASSEMBLER PROGRAM'
***********************************************************************
*   DESCRIPTION: THIS SAMPLE PROGRAM ISSUES A REQUEST TO THE
*                ENDEVOR API TO BUILD A LIST OF ENVIRONMENTS.
*
*   HOW TO USE:  POPULATE THE CONTROL AND REQUEST BLOCKS WITH THE
*                DESIRED VALUES.
*
*   HOW TO USE:  ASSEMBLE AND LINK-EDIT THIS MODULE
*
*   LINK EDIT:   AMODE=31,RMODE=24,RENT,REUS (RECOMMENDED)
*                OR
*                AMODE=24,RMODE=24,RENT,REUS
*
*   REGISTERS ON ENTRY:
*
*   REGISTER USAGE:
*                R2     -> SAVE RETURN CODE
*                R3     -> SAVE REASON CODE
*                R12    -> BASE PROGRAM
*                R13    -> STANDARD USAGE........
*                R15    -> RETURN CODE FROM CALL
*   ==>                 -> WE USE STANDARD STACK SAVEAREA USAGE
*
***********************************************************************
*   WORKAREA
***********************************************************************
WORKAREA DSECT
SAVEAREA DS    18F
WPARMLST DS    3A                      PARAMETER LIST
         DS     0D
         ENHAACTL DSECT=NO
         ENHALENV DSECT=NO
WORKLN   EQU   *-WORKAREA
***********************************************************************
*   REQISTER EQUATES
***********************************************************************
R0       EQU   0
R1       EQU   1
R2       EQU   2
R3       EQU   3
R4       EQU   4
R5       EQU   5
R6       EQU   6
R7       EQU   7
R8       EQU   8
R9       EQU   9
R10      EQU   10
R11      EQU   11
R12      EQU   12
R13      EQU   13
R14      EQU   14
R15      EQU   15
***********************************************************************
ENHAAPGM CSECT
ENHAAPGM AMODE 31
ENHAAPGM RMODE ANY
***********************************************************************
*   HOUSEKEEPING
***********************************************************************
         SAVE  (14,12)                 SAVE CALLERS REG 12(13)
         LR    R12,R15                 POINT TO THIS PROGRAM
        USING ENHAAPGM,R12
***********************************************************************
*   GET STORAGE FOR WORKAREA
***********************************************************************
         L     R0,=A(WORKLN)           GET SIZE OF W.A
         GETMAIN R,LV=(0)              GET WORKING STORAGE
         ST    R1,8(R13)               STORE NEW STACK +8(OLD)
         ST    R13,4(R1)               STORE OLD STACK +4(NEW)
         LR    R13,R1                  POINT R13 TO OUR STACK
        USING SAVEAREA,R13             ESTABLISH ADDRESSIBILIY
         SPACE ,
************************************************************
*        INITIALIZE AND POPULATE THE CONTROL STRUCTURE
*        NOTE: IF ANY INVENTORY MANAGEMENT MESSAGES ARE ISSUED, THEY
*        ARE WRITTEN TO THE MSG DATA SET. THE OUTPUT FROM THIS REQUEST
*        IS WRITTEN TO THE LIST DATA SET.
************************************************************
         API$INIT STG=AACTL,BLOCK=AACTL
         MVI   AACTL_SHUTDOWN,C'N'
         MVC   AACTL_MSG_DDN,=CL8'APIMSGS'
         MVC   AACTL_LIST_DDN,=CL8'APIEXTR'
*
************************************************************
*        INITIALIZE AND POPULATE THE REQUEST STRUCTURE
************************************************************
         API$INIT STG=ALENV_RQ,BLOCK=ALENV_RQ
         MVI   ALENV_RQ_PATH,C'L'             LOGICAL MAP
         MVI   ALENV_RQ_RETURN,C'A'           RETURN ALL INSTANCES
         MVI   ALENV_RQ_SEARCH,C'A'           SEARCH ENTIRE PATH
         MVC   ALENV_RQ_ENV,=CL8'INT'         STARTING ENV NAME
         MVC   ALENV_RQ_TOENV,=CL8'PRD'       ENDING ENV NAME
*
************************************************************
*        INITIALIZE THE RESPONSE STRUCTURE AND BUILD PARMLIST
************************************************************
         API$INIT STG=ALENV_RS,BLOCK=ALENV_RS
         LA    R1,WPARMLST
         LA    R14,AACTL
         ST    R14,0(0,R1)
         LA    R14,ALENV_RQ
         ST    R14,4(0,R1)
         LA    R14,ALENV_RS
         ST    R14,8(0,R1)
         OI    8(R1),X'80'
*
************************************************************
*        CALL THE ENDEVOR API INTERFACE PROGRAM
************************************************************
         L     R15,=V(ENA$NDVR)
         BALR  R14,R15
         LR    R2,R15                     HOLD ONTO THE RETURN CODE
         LR    R3,R0                      HOLD ONTO THE REASON CODE
************************************************************
* SHUTDOWN THE API SERVER. ONLY THE AACTL BLOCK IS REQUIRED.
************************************************************
         API$INIT STG=AACTL,BLOCK=AACTL
         MVI   AACTL_SHUTDOWN,C'Y'
         LA    R1,WPARMLST
         LA    R14,AACTL
         ST    R14,0(0,R1)
         OI    0(R1),X'80'
         L     R15,=V(ENA$NDVR)
         BALR  R14,R15
***********************************************************************
* PROGRAM EXIT
***********************************************************************
         LR    R5,R13                  SAVE SAVEAREA ADDRESS
         L     R13,4(R13)              POINT TO PREVIOUS SAVEAREA
*   CLEAN UP THIS PROGRAM'S STORAGE
         L     R0,=A(WORKLN)                GET SIZE
         FREEMAIN R,A=(R5),LV=(R0)          FREE STORAGE
         LR    R15,R2                       SET RETURN CODE FROM CALL
         L     R14,12(R13)
         LM    R0,R12,20(R13)
         BSM   0,R14                        RETURN
         END
